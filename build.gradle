import org.gradle.internal.os.OperatingSystem;

plugins {
    id 'org.beryx.runtime' version '1.12.7'
}

version = '1.0.0'
archivesBaseName = 'hitakieditor'
mainClassName = 'com.github.uguisu32j.hitakieditor.HitakiEditor'
applicationDefaultJvmArgs = ['-Dapple.laf.useScreenMenuBar=true', '-Djava.net.useSystemProxies=true']

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.formdev:flatlaf:1.6.5'
    implementation 'ch.qos.logback:logback-classic:1.2.10'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

runtime {
    jpackage {
        def os = OperatingSystem.current()
        if (os.windows) {
            installerType = 'exe'
            installerOptions = ['--win-dir-chooser', '--win-menu', '--win-per-user-install']
            // imageOptions = ['--icon', 'icons/icon.ico']
        } else if (os.macOsX) {
            installerType = 'dmg'
            imageOptions = ['--icon', 'icons/icon.icns']
        } else {
            imageOptions = ['--icon', 'icons/icon.png']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
